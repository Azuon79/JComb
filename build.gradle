

plugins {
    id 'java-library'
    id 'eclipse-wtp'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.8.4"
    id "com.github.hierynomus.license" version "0.15.0"
}

eclipse {
    project {
        name = 'JComb'
    }
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    
    implementation 'com.google.guava:guava:23.0'
    api 'org.junit.jupiter:junit-jupiter-api:5.3.2'
    
    testImplementation 'org.hamcrest:hamcrest-library:2.1'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
    testImplementation 'org.junit.platform:junit-platform-launcher:1.5.2'
    testImplementation "org.mockito:mockito-core:2.+"
}

repositories {
    jcenter()
}

license{
	header = file('licenseheader.txt')
	ext.year = '2019'
	ext.name = 'Noah Zuch'
	ext.email = 'noahz97@gmail.com'
	strictCheck true
}

//Publication

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'  
    from javadoc.destinationDir
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts{
	archives sourcesJar
	archives javadocJar
}

def pomConfig = {
	

	developers {
        developer {
        	id "zuchn"
            name "Noah Zuch"
            email "noahz97@gmail.com"
        }
    }
    
     scm {
        url "https://github.com/noahzuch/JComb"
    }
}

publishing {
    publications {
        myPublication(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            artifact javadocJar {
                classifier "javadoc"
            }
            groupId 'com.github.noahzuch.jcomb'
            artifactId 'jcomb'
            version '1.0.0'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', ' A JUnit extension for automated combinatorial testing.')
                root.appendNode('name', 'JComb')
                root.appendNode('url', 'https://github.com/noahzuch/JComb')
                def licenses = root.appendNode('licenses')
                def license = licenses.appendNode('license')
                license.appendNode('name', 'The Apache Software License, Version 2.0')
                license.appendNode('url', 'http://www.apache.org/licenses/LICENSE-2.0.txt')
                license.appendNode('distribution', 'repo')
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
	user = System.getProperty('bintray.user')
	key = System.getProperty('bintray.key')
	publications = ['myPublication']

	pkg {
		repo = 'JComb'
		name = 'jcomb'
		licenses = ['Apache-2.0']
		vcsUrl = 'https://github.com/noahzuch/JComb'
		version {
			name = '1.0.0'
			desc = '1.0.0'
			released  = new Date()
		}
	}

}

